import platform


# Make the "configfile" parameter optional,
# allowing the workflow to be triggered solely with CLI parameters
def recursetdefault(self: dict, defaults: dict) -> None:
    for key, val in defaults.items():
        if key not in self:
            self[key] = val
        elif isinstance(self[key], dict) and isinstance(val, dict):
            recursetdefault(self[key], val)


default_config = {
    "species": "human",
    "genome": {
        "url": "__placeholder__",
        "local": "resources/genome.fasta.gz",
    },
    "scan_region": {
        "upstream": [0, 0],
        "downstream": [1, 500],
    },
    "db": "",  # if empty use internal RBP motif
    "background": "",  # if empty use shuffled sequences
    "dna2rna": False,
}
# a way to change the I/O path pattern, though not elegant enough
advanced_default_config = {
    "exon": "{prefix}.bed",  # input
    "report": "results/xstreme/{prefix}",  # output
}

recursetdefault(config, default_config)
recursetdefault(config, advanced_default_config)

# infer the independent parameters
config["species_scientific_name"] = {
    "human": "Homo_sapiens",
    "mouse": "Mus_musculus",
}[config["species"]]

if not config["db"]:
    config["db"] = (
        "/opt/meme/share/meme-5.5.5/db/motif_databases/RNA/Ray2013_rbp_"
        + config.get("species_scientific_name")
        + (".meme" if config.get("dna2rna") else ".dna_encoded.meme")
    )


wildcard_constraints:
    prefix="[^/]+",


rule all:
    input:
        config["report"],


rule define_scan_region:
    input:
        config["exon"],
    output:
        "results/{prefix}.scan_region.bed",
    params:
        upstream=config["scan_region"].get("upstream", [0, 0]),
        downstream=config["scan_region"].get("downstream", [1, 500]),
    container:
        "docker://ghcr.io/ghbore/wf-rbp-motif:latest"
    script:
        "scripts/scanregion.py"


rule merge_bed:
    input:
        "results/{prefix}.scan_region.bed",
    output:
        "results/{prefix}.scan_region.merged.bed",
    container:
        "docker://ghcr.io/ghbore/wf-rbp-motif:latest"
    script:
        "scripts/mergebed.py"


rule download_genome:
    output:
        config["genome"]["local"],
    params:
        url=config["genome"]["url"],
    shell:
        """
        curl --progress-bar -o {output} {params.url}
        """


rule retrieve_seq:
    input:
        "results/{prefix}.scan_region.merged.bed",
        config["genome"].get("local"),
    output:
        "results/{prefix}.scan_region.fasta",
    container:
        "docker://ghcr.io/ghbore/wf-rbp-motif:latest"
    script:
        "scripts/retseq.py"


if platform.system() == "Darwin":

    rule run_xstreme_Darwin:
        input:
            "results/{prefix}.scan_region.fasta",
        output:
            directory(config["report"]),
        params:
            dna2rna=config.get("dna2rna", False),
            db=config.get("db"),
            background=config.get("background"),
        shell:
            """
            docker run --rm --user $(id -u):$(id -g) \
                -v $PWD:$PWD -w $PWD \
                memesuite/memesuite:latest \
                xstreme \
                    --p {input} \
                    --oc {output} \
                    $(test "{params.dna2rna}" == "True" && echo "--dna2rna") \
                    $(test -n "{params.background}" && test -f "{params.background}" && echo "--n {params.background}") \
                    --seed 1 \
                    --no-pgc \
                    --m {params.db}
            """

else:

    rule run_xstreme:
        input:
            "results/{prefix}.scan_region.fasta",
        output:
            directory(config["report"]),
        params:
            dna2rna=config.get("dna2rna"),
            db=config.get("db"),
            background=config.get("background"),
        container:
            "docker://memesuite/memesuite:latest"
        shell:
            """
            xstreme \
                --p {input} \
                --oc {output} \
                $(test "{params.dna2rna}" == "True" && echo "--dna2rna") \
                $(test -n "{params.background}" && test -f "{params.background}" && echo "--n {params.background}") \
                --seed 1 \
                --no-pgc \
                --m {params.db}
            """
